defmodule Fountain.Accounts.ApiKey do
  use Ecto.Schema
  import Ecto.Changeset

  @primary_key {:id, :binary_id, autogenerate: true}
  @foreign_key_type :binary_id

  @moduledoc """
  A tenant API key.

  ## Scopes

    * `"full"`   — everything, including issuing and revoking API keys. What a
      human gets from the UI or `fountain keys create`.
    * `"sprite"` — the per-conversation token handed to a sandbox. Deliberately
      permits the normal resource surface, because spawning sub-agents from
      inside a sprite is a supported workflow, but **not** API key management:
      without that exclusion, code running in a sandbox can mint a permanent
      key that survives the conversation-scoped revoke at teardown, turning a
      one-conversation credential into standing tenant access.
    * `"principal"` — the credential a claimable principal is operated with
      (ADR 0044), before and after it is claimed. Like `sprite` it is outside
      `@key_management_scopes`, so every `:require_full_scope` route refuses
      it: a principal cannot mint a further credential, change an account, buy
      credit, widen its own limits, or reach the `claimable-users` surface it
      was opened from. What it can do is build and run a computer — agents,
      environments, vaults, conversations, sandboxes, the team — which is the
      whole of what an anonymous visitor's application needs.
  """

  @scopes ~w(full sprite principal)

  # Scopes permitted to issue, list, or revoke API keys.
  @key_management_scopes ~w(full)

  @type t :: %__MODULE__{}
  schema "api_keys" do
    field :name, :string
    field :key_hash, :string
    field :key_prefix, :string
    field :last_used_at, :utc_datetime
    field :revoked_at, :utc_datetime
    field :expires_at, :utc_datetime
    field :scopes, {:array, :string}, default: ["full"]

    belongs_to :user, Fountain.Accounts.User

    timestamps(type: :utc_datetime)
  end

  def scopes, do: @scopes

  @doc "Whether `key` may issue, list, or revoke API keys."
  def may_manage_keys?(%__MODULE__{scopes: scopes}) do
    Enum.any?(scopes, &(&1 in @key_management_scopes))
  end

  @doc "Whether `key` is past its expiry. Keys without one never expire."
  def expired?(%__MODULE__{expires_at: nil}), do: false

  def expired?(%__MODULE__{expires_at: at}) do
    DateTime.compare(DateTime.utc_now(), at) == :gt
  end

  @doc """
  Changeset for creating a new API key record.
  Expects :name, :key_hash, :key_prefix, :user_id to be provided.
  The raw key is never stored — callers must compute key_hash and key_prefix before
  calling this changeset.
  """
  def changeset(api_key, attrs) do
    api_key
    |> cast(attrs, [:name, :key_hash, :key_prefix, :user_id, :scopes, :expires_at])
    |> validate_required([:name, :key_hash, :key_prefix, :user_id, :scopes])
    |> validate_length(:name, min: 1, max: 200)
    |> validate_length(:scopes, min: 1)
    |> validate_subset(:scopes, @scopes)
    |> require_principal_expiry()
    |> check_constraint(:expires_at, name: :api_keys_active_principal_expiry_required)
    |> unique_constraint(:key_hash)
    |> foreign_key_constraint(:user_id)
  end

  defp require_principal_expiry(changeset) do
    if "principal" in (get_field(changeset, :scopes) || []) and
         is_nil(get_field(changeset, :revoked_at)) do
      validate_required(changeset, [:expires_at])
    else
      changeset
    end
  end
end
